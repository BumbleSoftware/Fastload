plugins {
	id 'java'
	id 'idea'
	id 'eclipse'
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
}
loom {
	accessWidenerPath = file("src/main/resources/fastload.accesswidener")
}


dependencies {
	minecraft "com.mojang:minecraft:1.18.2"
	mappings "net.fabricmc:yarn:1.18.2+build.${project.yarn_mappings_1182}:v2"
	modImplementation(include(fabricApi.module("fabric-resource-loader-v0", "${project.fapi_1182}")))
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	implementation"it.unimi.dsi:fastutil:${project.fastutil_version}"
	modImplementation(include 'com.github.Fallen-Breath:conditional-mixin:v0.3.2')
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	afterEvaluate {
		subprojects.each {
			include project("${it.name}:")
		}
	}

}

allprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	archivesBaseName = project.archives_base_name
	version = project.mod_version
	group = project.maven_group

	sourceSets {
		testmod {
			compileClasspath += main.compileClasspath
			runtimeClasspath += main.runtimeClasspath
		}
	}

	repositories {
		maven { url = "https://maven.terraformersmc.com/" }
		maven { url = "https://maven.fabricmc.net/" }
		maven { url 'https://jitpack.io' }
	}

	configurations {
		dev {
			canBeResolved = false
			canBeConsumed = true
		}
	}

	processResources {
		inputs.property "version", project.version

		filesMatching("fabric.mod.json") {
			expand "version": project.version
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 17
	}

	java {
		withSourcesJar()
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${project.archivesBaseName}" }
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}

		repositories {
		}
	}
}

subprojects {
	dependencies {
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
		modImplementation 'com.github.Fallen-Breath:conditional-mixin:v0.3.2'
		implementation project(path: ':', configuration: "namedElements")
	}

	version = rootProject.version
}
